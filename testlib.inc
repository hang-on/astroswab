; testlib.inc
; Macros, functions and data for testing library.

; -----------------------------------------------------------------------------
;                         M A C R O S
; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------
.macro assertEqualsA ; (value, fail string)
; -----------------------------------------------------------------------------
  cp \1
  jp z,+
    fail \2
  +:
.endm

; -----------------------------------------------------------------------------
.macro assertNotEqualsA ; (value, fail string)
; -----------------------------------------------------------------------------
  cp \1
  jp nz,+
  fail \2
  +:
.endm

; -----------------------------------------------------------------------------
.macro assertEqualsHL ; (value, fail string)
; -----------------------------------------------------------------------------
  ld de,\1
  ld a,d
  cp h
  jp nz,+
  ld a,e
  cp l
  jp z,++
  +:
    fail \2
  ++:
.endm

; -----------------------------------------------------------------------------
.macro assertEqualsByte ; (byte-sized variable, value, fail string)
; -----------------------------------------------------------------------------
  ld a,(\1)
  ld b,\2
  cp b
  jp z,+
    fail \3
  +:
  log "."
.endm

; -----------------------------------------------------------------------------
.macro assertEqualsWord ; (word-sized variable, value, fail string)
; -----------------------------------------------------------------------------
  ld hl,(\1)
  ld de,\2
  sbc hl,de
  jp z,+
    fail \3
  +:
.endm


; -----------------------------------------------------------------------------
.macro assertCarrySet ; (fail string)
; -----------------------------------------------------------------------------
  jp c,+
    fail \1
  +:
.endm

; -----------------------------------------------------------------------------
.macro assertCarryReset ; (fail string)
; -----------------------------------------------------------------------------
  jp nc,+
    fail \1
  +:
.endm

; -----------------------------------------------------------------------------
.macro fail ; (fail string)
; -----------------------------------------------------------------------------
  log "Assertion failed:               --> "
  log \1
  abort
.endm

; -----------------------------------------------------------------------------
.macro log
; -----------------------------------------------------------------------------
  ld a,(logger_current_row)
  ld b,a
  ld hl,logData\@
  call print
  ld hl,logger_current_row
  inc (hl)
  jp logDataEnd\@
  logData\@:
    .asc "> "
    .asc \1
    .asc "#"
  logDataEnd\@:
.endm

; -----------------------------------------------------------------------------
.macro abort
; -----------------------------------------------------------------------------
    .if nargs == 1
      log \1
    .endif
    -:
      nop
    jp -
.endm

; -----------------------------------------------------------------------------
.ramsection "testlib variables" slot 3
; -----------------------------------------------------------------------------
  logger_first_row db
  logger_current_row db
  ;
.ends

; -----------------------------------------------------------------------------
.section "reset_logger" free
; -----------------------------------------------------------------------------
  ; Reset logger.
  ; Entry: B = logger start row
  ; Exit: None.
  ; Uses: AF, BC, HL
  ;       logger_current_row, logger_first_row
  reset_logger:
    ; Save initial row data.
    ld a,b
    inc a
    ld (logger_first_row),a
    ld (logger_current_row),a
    ; Make divider.
    ld c,0
    ld hl,logger_reset_msg
    call print
  ret
  ;
  logger_reset_msg:
    .asc "--------------------------------#"
.ends
